//===== rAthena Script =======================================
//= Build Manager +
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.8
//===== Description: =========================================
//= Armazena status de diferentes builds para serem trocados com facilidade.
//============================================================

prontera,143,170,6	script	Trocador de Atributos	930,{
function Get_Menu; function Save_Build; function Load_Build;

	set .@BuildCount,4;	// Número máximo de builds armazenadas
	set .@Save,5000;	// Zeny requirido para salvar a build
	set .@Load,10000;	// Zeny requirido para carregar a build
	set .@Rename,1000;	// Zeny requirido para renomear a build
	set .@MaxLevel,300;	// Nível máximo do servidor (para prevenir abusos de status)

	if (BaseLevel < .@MaxLevel) { message strcharinfo(0),"Você precisa ser "+.@MaxLevel+" para poder trocar seus atributos."; end; }
	switch(select("Salvar Build...:Carregar Build...:Renomear Build...:Nada")) {
		case 1:
			if (.@Save) message strcharinfo(0),"Requerimento: "+.@Save+" Zeny para salvar a build.";
			set .@Build, Get_Menu(.@BuildCount);
			if (Zeny<.@Save) { message strcharinfo(0),"Zeny insuficiente, volte mais tarde."; close; }
			if (getd("Build_"+.@Build+"$")!="") {
				message strcharinfo(0),"Desaja substituir a build #"+.@Build+"?";
				if(select("Salvar nova build:Cancelar")==2) close; }
			Save_Build(.@Build);
			message strcharinfo(0),"Digite um nome para a sua build.";
			input getd("Build_"+.@Build+"n$");
			message strcharinfo(0),"Build #"+.@Build+" ("+getd("Build_"+.@Build+"n$")+") salva com sucesso!.";
			set Zeny, Zeny-.@Save;
			close;
		case 2:
			if (.@Load) message strcharinfo(0),"Requerimento: "+.@Load+" Zeny para carregar a build";
			set .@Build, Get_Menu(.@BuildCount);
			if (getd("Build_"+.@Build+"$")=="") {
				message strcharinfo(0),"Nenhuma build encontrada."; close; }
			if (Zeny<.@Load) { message strcharinfo(0),"Zeny insuficiente, volte mais tarde."; close; }
			Load_Build(getd("Build_"+.@Build+"$"));
			message strcharinfo(0),"Build #"+.@Build+" carregada.";
			set Zeny, Zeny-.@Load;
			close;
		case 3:
			if (.@Rename) message strcharinfo(0),"Requerimento: "+.@Rename+" Zeny para renomear build.";
			set .@Build, Get_Menu(.@BuildCount);
			if (getd("Build_"+.@Build+"$")=="") {
				message strcharinfo(0),"Nenhuma build encontrada."; close; }
			if (Zeny<.@Rename) { message strcharinfo(0),"Zeny insuficiente, volte mais tarde."; close; }
			message strcharinfo(0),"Digite o novo nome da build #"+.@Build+" ("+getd("Build_"+.@Build+"n$")+").";
			input getd("Build_"+.@Build+"n$");
			message strcharinfo(0),"Build #"+.@Build+" renomeada.";
			set Zeny, Zeny-.@Rename;
			close;
		case 4:
			close; }
	function Get_Menu {
		set .@menu$,"";
		for(set .@i,1; .@i<=getarg(0); set .@i,.@i+1)
			set .@menu$, .@menu$+"Slot "+.@i+" ("+((getd("Build_"+.@i+"n$")=="")?"^777777vazio":"^0055FF"+getd("Build_"+.@i+"n$"))+"^000000):";
		return select(.@menu$); }
	function Save_Build {
		set .@s$,"";
		for(set .@i,13; .@i<19; set .@i,.@i+1)
			set .@s$,.@s$+readparam(.@i)+"|";
		setd "Build_"+getarg(0)+"$", .@s$+StatusPoint;
		return; }
	function Load_Build {
		resetstatus;
		explode(.@s$,getarg(0),"|");
		for(set .@i,0; .@i<6; set .@i,.@i+1)
			statusup2 (.@i+13), atoi(.@s$[.@i])-1;
		set StatusPoint, atoi(.@s$[6]);
		return; }
}