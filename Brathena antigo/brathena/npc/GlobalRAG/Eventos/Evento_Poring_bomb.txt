// .----------------------------------------------------------.
// .                ___________________                       .
// .               /   _____/\______   \                      .
// .               \_____  \  |    |  _/                      .
// .               /        \ |    |   \                      .
// .              /_______  / |______  /                      .
// .                      \/         \/                       .
// .               [ Advanced Scripts ]                      .
// .                                                    v1.2  .
// .----------------------------------------------------------.
// ;               https:github.com/SoulBlaker/               ;
// .----------------------------------------------------------.
// . Script:                                                  .
// .  Evento Bomb Poring                                      .
// .----------------------------------------------------------.
// . Por: SoulBlaker   Compátivel: Cronus       versão: 1.0   .
// .----------------------------------------------------------.
// . Descrição:                                               .
// .  Arena de Evento Bomb Poring, no qual os jogadores       .
// .  devem correr de Bomb Poring e sobreviver até o final.   .
// .----------------------------------------------------------.
// . Changelog da Versão:                                     .
// .  * 1.0 [SoulBlaker]                                      .
// .	- Iniciado NPC.                                       .
// *----------------------------------------------------------*
prontera,140,167,5	script	Evento Bomb Poring#warp	4_M_JOB_ASSASSIN,{
	set .@loop,1;
	while(.@loop)
	{
		mes "^FF0000[Recepcionista]^000000";
		mes "Bem-vindo ao ^0000FFBomb Poring^000000.";
		if( $@ev_bombporing_flag >= 2 )
			mes "Neste momento está ocorrendo o evento,";
		else if( $@ev_bombporing_flag == 1 )
			mes "Neste momento a arena do evento está aberto,";
		else
			mes "Neste momento, não está acontecendo o evento,";
		mes "em que posso ajudar?";
		next;
		switch( select(($@ev_bombporing_flag==1?"^FF0000- Entrar no Evento.^000000":""), "^FF0000- Informações.^000000", "^0000FF- Cancelar.^000000") )
		{
			case 1:
				if( !$@ev_bombporing_flag || $@ev_bombporing_flag >= 2 )
				{
					mes "^FF0000[Recepcionista]^000000";
					mes "O Evento ainda não começou e você não pode acessar a arena.";
					next;
					break;
				}
				
				mes "^FF0000[Recepcionista]^000000";
				mes "Tem certeza que deseja entrar na Arena do Evento?";
				next;
				if( select("- Sim, por favor.", "- Não, obrigado.") == 1 )
				{
					mes "^FF0000[Recepcionista]^000000";
					mes "Muito bem, vou te mandar para a arena do evento!";
					close2;
					warp "quiz_01", 52, 45;
					end;
				}
				break;
			case 2:
				mes "^FF0000[Recepcionista]^000000";
				mes "Neste evento Bomb Poring's são invocado e o objetivo dos jogadores é sobreviver das explosões deles.";
				mes "Apenas 1 jogador é vencedor deste evento, e é nescessário do minimo de 2 jogadores para que ele seja realizado.";
				mes "As Habilidades são desativada no mapa do Evento.";
				mes " ";
				mes "Para saber o Plano de Horários de Eventos, acesse o link abaixo:";
				mes "www.portalrag.com/forum";
				next;
				break;
			case 3:
				set .@loop, 0;
				break;
		}
	}
	mes "^FF0000[Recepcionista]^000000";
	mes "Tudo bem, volte quando desejar entrar no evento.";
	close;
	
OnDisable:
	delwaitingroom;
	end;

OnEnable:
	waitingroom "Aberto o Evento", 0;
	end;
}

// Recompensadores.
quiz_01,52,45,0	script	Recompensador#ev_bombporing	4_M_JOB_ASSASSIN,{
	if( $@ev_bombporing_flag != 3 ) end;
	
	/*setarray .@rewardid[0], 30068, 30064, 30069, 30063;
	setarray .@rewardamount[0], 1, 1, 1;
	
	set .@rand, rand((getarraysize(.@rewardid)-1));
	if( !checkweight(.@rewardid[.@rand],.@rewardamount[.@rand]) )
	{
		mes "^FF0000[Recepcionista]^000000";
		mes "Você está carregando muito peso com você, irei abrir o Armazém para você guardar alguns de seus itens.";
		close2;
		openstorage();
		end;
	}*/
	dispbottom "Você venceu! Prêmio: 100 Cash e 5 Bilhete de Armazem";
	set #CASHPOINTS,#CASHPOINTS+100;
	getitem 19008,5;
	/* 
	getitem .@rewardid[.@rand], .@rewardamount[.@rand];
	mes "^FF0000[Recepcionista]^000000";
	mes "Parabéns, você acaba de receber ^0000FF" + .@rewardamount[.@rand] + "^000000x " + getitemname(.@rewardid[.@rand]) + ".";
	close2;
	*/
	donpcevent "event#ev_bombporing::OnEnd";
	end;
	
OnInit:
	disablenpc strnpcinfo(0);
	end;
}

quiz_01,42,287,0	script	Juíz do Evento#ev_bombporing	4_M_JOB_ASSASSIN,{
	if( $@ev_bombporing_flag )
	{
		switch(rand(0,3))
		{
			default:
			case 0:
				npctalk "Fique de olho nos Bomb Poring, eles pode te derrotar fácilmente.";
				break;
			case 1:
			case 2:
				npctalk "Concentre-se, não vai querer perder esta partida neh?";
				break;
		}
	}
	end;
	
OnInit:
	disablenpc strnpcinfo(0);
	end;
}

// Scripts de Evento
-	script	event#ev_bombporing	-1,{
	
OnStart:
	set $@ev_bombporing_flag, 1;
	set $@ev_bombporing_timer, 1;
	enablenpc "Juíz do Evento#ev_bombporing";
	announce "[Bomb Poring]: O Evento vai começar em " + $@ev_bombporing_timer + " minutos, corram para a arena!", bc_all, "0x00FF00";
	initnpctimer;
	donpcevent "Evento Bomb Poring#warp::OnEnable";
	end;

OnTimer100000:
	set $@ev_bombporing_timer, $@ev_bombporing_timer - 1;
	if( $@ev_bombporing_timer > 0 )
	{
		announce "[Bomb Poring]: O Evento vai começar em " + $@ev_bombporing_timer + " minutos, corram para a arena!", bc_all, "0x00FF00";
		setnpctimer 0;
	}
	else {
		stopnpctimer;
		if( getmapusers("quiz_01") < 2 )
		{
			announce "[Bomb Poring]: O Evento foi cancelado por não haver jogadores suficiente na arena.", bc_all, "0x00FF00";
			donpcevent "event#ev_bombporing::OnEnd";
		}
		else {
			set $@ev_bombporing_flag, 2;
			set $@ev_bombporing, 7;
			set $@ev_bombporing_players, getmapusers("quiz_01");
			donpcevent "mobspawn#ev_bombporing::OnMobSpawnDie";
			initnpctimer "timer#ev_bombporing";
		}
		donpcevent "Evento Bomb Poring#warp::OnDisable";
	}
	end;
	
OnDownEvent:
	announce "[Bomb Poring]: Todos jogadores falharam no Bomb Poring!", bc_all, "0x00FF00";
	donpcevent "event#ev_bombporing::OnEnd";
	end;
	
OnFinishEvent:
	mapannounce "quiz_01", "[Bomb Poring]: Você é o vencedor, diriga-se ao recompensador!", bc_all, "0x00FF00";
	set $@ev_bombporing_flag, 3;
	enablenpc "Recompensador#ev_bombporing";
	end;
	
OnEndGM:
	announce "[Bomb Poring]: O Evento foi Cancelado pelo GameMaster de Eventos.", bc_all, "0x00FF00";
	stopnpctimer;
OnEnd:
	set $@ev_bombporing, 0;
	set $@ev_bombporing_flag, 0;
	mapwarp "quiz_01", "prontera", 155, 179;
	end;

OnPCDieEvent:
	if( $@ev_bombporing_flag && strcharinfo(3) == "quiz_01" )
	{
		set $@ev_bombporing_players, $@ev_bombporing_players - 1;
		warp "prontera", 155, 179;
	}
	end;
}
-	script	mobspawn#ev_bombporing	-1,{
	
OnMobSpawn:
	sleep 2000;
	areamonster "quiz_01",23,39,292,277,"Evento Bomb Poring",1904,$@ev_bombporing,"mobspawn#ev_bombporing::OnMobSpawnDie";
	sleep 5000;
	areamonster "quiz_01",45,61,293,277,"Evento Bomb Poring",1904,$@ev_bombporing,"mobspawn#ev_bombporing::OnMobSpawnDie";
	set $@ev_bombporing, $@ev_bombporing + 7;
	end;
	
OnMobSpawnDie:
	set .@mobcount, mobcount("quiz_01","mobspawn#ev_bombporing::OnMobSpawnDie");
	if( $@ev_bombporing_players == 1 )
	{
		killmonsterall "quiz_01";
		donpcevent "event#ev_bombporing::OnFinishEvent";
	}
	else if( $@ev_bombporing_flag && $@ev_bombporing_players <= 0 )
	{
		killmonsterall "quiz_01";
		donpcevent "event#ev_bombporing::OnDownEvent"; 
	}
	else if( $@ev_bombporing_flag && .@mobcount <= 0 )
	{
		switch( $@ev_bombporing )
		{
			case 7:
				mapannounce "quiz_01", "[Bomb Poring]: Vamos começar, cuidado com os Bomb Poring invocados!", bc_all, "0x00FF00";
				break;
			case 14:
				mapannounce "quiz_01", "[Bomb Poring]: Essa foi fácil, que tal dobrarmos a quantidade?", bc_all, "0x00FF00";
				break;
			case 21:
				mapannounce "quiz_01", "[Bomb Poring]: Está ficando meio tenso?", bc_all, "0x00FF00";
				break;
			default:
				mapannounce "quiz_01", "[Bomb Poring]: Incrível, mais que tal melhorarmos as coisas? " + $@ev_bombporing + " Bomb Porings.", bc_all, "0x00FF00";
				break;
		}
		donpcevent "mobspawn#ev_bombporing::OnMobSpawn";
	}
	end;
}

-	script	timer#ev_bombporing	-1,{
OnTimer60000:
	if( !getmapusers("quiz_01") )
		donpcevent "event#ev_bombporing::OnEnd";
	else
		setnpctimer 0;
	end;
}
-	script	Cmd_Event	-1,{
OnClock1000:
OnClock1500:
OnClock1800:
donpcevent "event#ev_bombporing::OnStart";
end;
}

quiz_01	mapflag	noteleport
quiz_01	mapflag	nowarp
quiz_01	mapflag	nowarpto
quiz_01	mapflag	noreturn
quiz_01	mapflag	nosave
quiz_01	mapflag	noskill